#!/bin/zsh

# Skip all this for non-interactive shells
[ -z "$PS1" ] && return

# [[[ Config
# Title
case $TERM in
  screen*)
      precmd() { print -Pn "\033k%~\033\\" }
      preexec() { print -Pn "\033k$1\033\\" }
    ;;
  rxvt*)
      precmd() { print -Pn "\e]2;%~ (%n@%m)\a" }
      preexec() { print -Pn "\e]2;$1 (%n@%m)\a" }
    ;;
esac

# History
HISTFILE=~/.zhistory
HISTSIZE=100000
SAVEHIST=100000
setopt append_history
setopt extended_history
setopt hist_ignore_dups
setopt inc_append_history
setopt share_history

# Misc zsh settings
export FPATH="$HOME/.zsh/:$FPATH"
setopt auto_resume
setopt long_list_jobs
setopt nobeep
setopt nohup
setopt notify
unsetopt bg_nice
unsetopt check_jobs

export EDITOR="vim"
export PAGER="less"

export GREP_OPTIONS='--color=auto'
export LESS="-FiXr"

# Less color
export LESS_TERMCAP_mb=$'\E[01;31m'
export LESS_TERMCAP_md=$'\E[01;31m'
export LESS_TERMCAP_me=$'\E[0m'
export LESS_TERMCAP_se=$'\E[0m'
export LESS_TERMCAP_so=$'\E[00;47;30m'
export LESS_TERMCAP_ue=$'\E[0m'
export LESS_TERMCAP_us=$'\E[01;32m'

# Disable ctrl-q and ctrl-s
stty -ixon
# ]]]
# [[[ Aliases
# Give some commands fancy colors
alias ls='ls -hF --si --color=auto --group-directories-first'
alias tree='tree -C'
alias pacman='pacman-color'

# Some for ls
alias l='ls'
alias ll='ls -l'
alias la='ls --almost-all'
alias lla='ls -l --almost-all'

# Up up and away
alias '..'='cd ..'
alias '...'='cd ../..'
alias '....'='cd ../../..'
alias '.....'='cd ../../../..'

# Bazinga
alias :q='echo you are not in vim anymore'
alias :w='echo you are not in vim anymore'

# Other
alias bell="echo -e '\a'"
alias calc='noglob calc'
alias g='git'
alias aur='aura'
alias ctl='systemctl'
# ]]]
# [[[ Binds
bindkey '' history-search-backward
bindkey '' history-search-forward
bindkey '' history-incremental-search-backward
bindkey '' push-line
# ]]]
# [[[ Prompt
autoload -U promptinit
autoload -U colors && colors

setopt prompt_subst

prompt_daoo_setup() {
  local colors && set -A colors cyan magenta blue yellow green grey
  local i=$((1 + $(echo -n $HOST | cksum | cut -d ' ' -f 1) % ${#colors}))
  local prompt_color=$fg[$colors[$i]]

  local color_start="%{%F${prompt_color}%}"
  local color_end="%{%f%}"

  local special_start="%{%B%F{black}%}"
  local special_end="%{%b%}"

  local l_bracket="${special_start}[${special_end}"
  local r_bracket="${special_start}]${special_end}"
  local l_paren="$special_start($special_end"
  local r_paren="$special_start)$special_end"
  local at_char="$special_start@$special_end"
  local hyphen="${color_start}-${color_end}"

  local dir="${l_paren}%{%B%F{magenta}%}%~%{%f%b%}${r_paren}"
  local host_info="${l_paren}%n${at_char}%m${r_paren}"
  local time="${l_bracket}%D${special_start}|${special_end}%*${r_bracket}"

  line1_a="${hyphen}${dir}${color_start}"
  line1_b="${color_end}${time}${hyphen}${host_info}${color_start}-"
  line2="--${color_end}%F{white}> "

  autoload add-zsh-hook
  add-zsh-hook precmd prompt_daoo_precmd
}

prompt_expanded_length() {
  printf ${#${(S%%)1//\%\{*\%\}}}
}

prompt_daoo_precmd() {
  local line1_a_width=$(prompt_expanded_length $line1_a)
  local line1_b_width=$(prompt_expanded_length $line1_b)

  local fill_width=$(($COLUMNS - $line1_a_width - $line1_b_width))
  local fill="${(l:$fill_width::-:)}"

  PS1="${line1_a}${fill}${line1_b}${line2}%b%f"
}

prompt_daoo_setup
# ]]]
# [[[ Auto completion
autoload -Uz compinit && compinit -i

setopt always_to_end       # Move cursor to the end of a completed word.
setopt auto_menu           # Show completion menu on a succesive tab press.
setopt auto_list           # Automatically list choices on ambiguous completion.
setopt auto_param_slash    # If completed parameter is a directory, add a trailing slash.
unsetopt menu_complete     # Do not autoselect the first completion entry.
unsetopt flow_control      # Disable start/stop characters in shell editor.

WORDCHARS='*?_-.[]~=&;!#$%^(){}<>'

zstyle ':completion::complete:*' use-cache on
zstyle ':completion::complete:*' cache-path "/tmp/zcache"

# Group matches and describe.
zstyle ':completion:*' format ' %F{yellow}-- %d --%f'
zstyle ':completion:*' group-name ''
zstyle ':completion:*' verbose yes
zstyle ':completion:*:*:*:*:*' menu select
zstyle ':completion:*:corrections' format ' %F{green}-- %d (errors: %e) --%f'
zstyle ':completion:*:default' list-prompt '%S%M matches%s'
zstyle ':completion:*:descriptions' format ' %F{yellow}-- %d --%f'
zstyle ':completion:*:matches' group 'yes'
zstyle ':completion:*:messages' format ' %F{purple} -- %d --%f'
zstyle ':completion:*:options' auto-description '%d'
zstyle ':completion:*:options' description 'yes'
zstyle ':completion:*:warnings' format ' %F{red}-- no matches found --%f'

# Don't complete unavailable commands.
zstyle ':completion:*:functions' ignored-patterns '(_*|pre(cmd|exec))'

# Array completion element sorting.
zstyle ':completion:*:*:-subscript-:*' tag-order indexes parameters

# Directories
zstyle ':completion:*' list-colors "${(s.:.)LS_COLORS}"
zstyle ':completion:*' squeeze-slashes true
zstyle ':completion:*:*:cd:*' tag-order local-directories directory-stack path-directories
zstyle ':completion:*:*:cd:*:directory-stack' menu yes select
zstyle ':completion:*:-tilde-:*' group-order 'named-directories' 'path-directories' 'users' 'expand'

# Populate hostname completion.
zstyle -e ':completion:*:hosts' hosts 'reply=(
  ${=${${(f)"$(cat {/etc/ssh_,~/.ssh/known_}hosts(|2)(N) 2>/dev/null)"}%%[#| ]*}//,/ }
  ${=${(f)"$(cat /etc/hosts(|)(N) <<(ypcat hosts 2>/dev/null))"}%%\#*}
  ${=${${${${(@M)${(f)"$(cat ~/.ssh/config 2>/dev/null)"}:#Host *}#Host }:#*\**}:#*\?*}}
)'

# Ignore multiple entries.
zstyle ':completion:*:(rm|kill|diff):*' ignore-line yes
zstyle ':completion:*:rm:*' file-patterns '*:all-files'

# Kill
zstyle ':completion:*:*:*:*:processes' command 'ps -u $USER -o pid,user,comm -w'
zstyle ':completion:*:*:kill:*' force-list always
zstyle ':completion:*:*:kill:*' insert-ids single
zstyle ':completion:*:*:kill:*' menu yes select
zstyle ':completion:*:*:kill:*:processes' list-colors '=(#b) #([0-9]#) ([0-9a-z-]#)*=01;36=0=01'

# Man
zstyle ':completion:*:manuals' separate-sections true
zstyle ':completion:*:manuals.(^1*)' insert-sections true
# ]]]
# [[[ Local settings
if [ -f ~/.zlocal ]; then
  . ~/.zlocal
fi
# ]]]

# vim: foldmarker=[[[,]]] fdm=marker :
